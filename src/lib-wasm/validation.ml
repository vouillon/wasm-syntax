(*
ZZZ
- tag validation
- resolve all type uses
- store text types on the stack for improved error reporting
*)

let validate_refs = ref true

module Uint32 = Utils.Uint32
module Uint64 = Utils.Uint64
open Ast.Binary.Types

let ( let*@ ) = Option.bind
let ( let+@ ) o f = Option.map f o
let ( let>@ ) o f = Option.iter f o

let print_heaptype f (ty : heaptype) =
  match ty with
  | Func -> Format.fprintf f "func"
  | NoFunc -> Format.fprintf f "nofunc"
  | Exn -> Format.fprintf f "exn"
  | NoExn -> Format.fprintf f "noexn"
  | Extern -> Format.fprintf f "extern"
  | NoExtern -> Format.fprintf f "noextern"
  | Any -> Format.fprintf f "any"
  | Eq -> Format.fprintf f "eq"
  | I31 -> Format.fprintf f "i31"
  | Struct -> Format.fprintf f "struct"
  | Array -> Format.fprintf f "array"
  | None_ -> Format.fprintf f "none"
  | Type id -> Format.fprintf f "%d" id

let print_valtype f ty =
  match ty with
  | I32 -> Format.fprintf f "i32"
  | I64 -> Format.fprintf f "i64"
  | F32 -> Format.fprintf f "f32"
  | F64 -> Format.fprintf f "f64"
  | V128 -> Format.fprintf f "v128"
  | Ref { nullable; typ } ->
      if nullable then
        Format.fprintf f "@[<1>(ref@ null@ %a)@]" print_heaptype typ
      else Format.fprintf f "@[<1>(ref@ %a)@]" print_heaptype typ
  | Tuple _ -> Format.fprintf f "tuple"

let print_string f s =
  let s = s.Ast.desc in
  let len, s = Output.escape_string s in
  Format.pp_print_as f len s

let print_ident f id =
  if Lexer.is_valid_identifier id then Format.fprintf f "$%s" id
  else Format.fprintf f "$\"%s\"" (snd (Misc.escape_string id))

let print_index f (idx : Ast.Text.idx) =
  match idx.desc with
  | Num n -> Format.fprintf f "%s" (Uint32.to_string n)
  | Id id -> print_ident f id

let print_text f s =
  Format.fprintf f "%a"
    (Format.pp_print_list
       ~pp_sep:(fun f () -> Format.pp_print_space f ())
       Format.pp_print_string)
    (String.split_on_char ' ' s)

module Error = struct
  open Utils

  let unbound_label context ~location id =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f "The label %a is not bound." print_index id)

  let unbound_index context ~location kind id =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f "The %s index %a is not bound." kind print_index id)

  let packed_array_access context ~location =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f
          "This instruction cannot be used on packed arrays. Use array.get_s \
           or array.get_u to specify sign extension.")

  let unpacked_array_access context ~location =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f
          "This instruction is only valid for packed arrays. Use array.get.")

  let packed_struct_access context ~location =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f
          "This instruction cannot be used on packed fields. Use struct.get_s \
           or struct.get_u to specify sign extension.")

  let unpacked_struct_access context ~location =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f
          "This instruction is only valid for packed fields. Use struct.get.")

  let instruction_type_mismatch context ~location ty' ty =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f
          "This instruction is expected to have type@ @[<2>%a@]@ but has type@ \
           @[<2>%a@]"
          print_valtype ty print_valtype ty')

  let expected_ref_type context ~location ~src_loc ty =
    match src_loc with
    | None ->
        Diagnostic.report context ~location ~severity:Error
          ~message:(fun f () ->
            Format.fprintf f "Expected reference type but got type@ @[<2>%a@]."
              print_valtype ty)
    | Some location ->
        Diagnostic.report context ~location ~severity:Error
          ~message:(fun f () ->
            Format.fprintf f
              "This instruction should return a reference type but has type@ \
               @[<2>%a@]."
              print_valtype ty)

  let table_type_mismatch context ~location idx ty =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f "The table %a@ %a@ @[%a@]." print_index idx print_text
          "should contain functions but its elements have type" print_valtype ty)

  let type_mismatch context ~location ty' ty =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f "Expecting type@ @[<2>%a@]@ but got type@ @[<2>%a@]."
          print_valtype ty print_valtype ty')

  let br_cast_type_mismatch context ~location =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f
          "The first type must be a supertype of the seconde one.")

  let select_type_mismatch context ~location ty1 ty2 =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f
          "Both branch of a select should have the same type.@ Here, they have \
           type@ @[<2>%a@]@ and@ @[<2>%a@]."
          print_valtype ty1 print_valtype ty2)

  let empty_stack context ~location =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f "The stack is empty.")

  let non_empty_stack context ~location output_stack =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f "Some values remain on the stack:%a" output_stack ())

  let branch_parameter_count_mismatch context ~location label len label' len' =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f
          "The default branch target@ %a@ expects@ %d@ parameters, while \
           branch target@ %a@ expects@ %d@ parameters."
          print_index label len print_index label' len')

  let memory_offset_too_large context ~location max_offset =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f "The memory offset should be less than 0x%Lx."
          (Uint64.to_int64 max_offset))

  let memory_align_too_large context ~location natural =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f
          "The memory alignment is larger than the natural alignment %d."
          natural)

  let bad_memory_align context ~location =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f "The memory alignment should be a power of two.")

  let invalid_lane_index context ~location max_lane =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f "The lane index should be less than %d." max_lane)

  let inline_function_type_mismatch context ~location _ =
    (*ZZZ print expected type *)
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f
          "The inline function type does not match the type definition.")

  let constant_expression_required context ~location =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f "Only constant expressions are allowed here.")

  let immutable_global context ~location idx =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f "The global %a should be mutable." print_index idx)

  let limit_too_large context ~location kind max =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f
          "The %s size it too large. It should be less than 0x%Lx." kind
          (Uint64.to_int64 max))

  let limit_mismatch context ~location kind =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f
          "The %s maximum size should be larger than the minimal size." kind)

  let duplicated_export context ~location name =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f "There is already an export of name \"%a\"."
          print_string name)

  let import_after_definition context ~location kind =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f "This imports is after a %s definition." kind)

  let supertype_mismatch context ~location =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f "The supertype is not of the same kind as this type.")

  let non_nullable_table_type context ~location =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f
          "The type of the elements of this table must be nullable.")

  let uninitialized_local context ~location idx =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f "The local variable %a has not been initialized."
          print_index idx)

  let index_already_bound context ~location kind index =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f "The %s index %a is already bound." kind print_ident
          index.Ast.desc)

  let expected_func_type context ~location idx =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f "Type %a should be a function type." print_index idx)

  let expected_struct_type context ~location idx =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f "Type %a should be a struct type." print_index idx)

  let expected_array_type context ~location idx =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f "Type %a should be an array type." print_index idx)

  let unsupported_tuple_type context ~location =
    Diagnostic.report context ~location ~severity:Error ~message:(fun f () ->
        Format.fprintf f "Tuple types are not supported yet.")
end

let print_instr f i = Utils.Printer.run f (fun p -> Output.instr p i)

module Sequence = struct
  type 'a t = {
    name : string;
    index_mapping : (int, 'a) Hashtbl.t;
    label_mapping : (string, int) Hashtbl.t;
    mutable last_index : int;
  }

  let make name =
    {
      name;
      index_mapping = Hashtbl.create 16;
      label_mapping = Hashtbl.create 16;
      last_index = 0;
    }

  let register seq id v =
    let idx = seq.last_index in
    seq.last_index <- seq.last_index + 1;
    Hashtbl.add seq.index_mapping idx v;
    Option.iter (fun id -> Hashtbl.add seq.label_mapping id.Ast.desc idx) id

  let get d seq (idx : Ast.Text.idx) =
    try
      match idx.desc with
      | Num n -> Some (Hashtbl.find seq.index_mapping (Uint32.to_int n))
      | Id id ->
          Some
            (Hashtbl.find seq.index_mapping (Hashtbl.find seq.label_mapping id))
    with Not_found ->
      Error.unbound_index d ~location:idx.info seq.name idx;
      None

  let get_index seq (idx : Ast.Text.idx) =
    match idx.desc with
    | Num n -> Uint32.to_int n
    | Id id -> (
        try Hashtbl.find seq.label_mapping id
        with Not_found -> assert false (* Should not happen *))
end

type type_context = {
  types : Types.t;
  mutable last_index : int;
  index_mapping : (Uint32.t, int * (string * int) list) Hashtbl.t;
  label_mapping : (string, int * (string * int) list) Hashtbl.t;
}

let get_type_info d ctx (idx : Ast.Text.idx) =
  try
    match idx.desc with
    | Num x -> Some (Hashtbl.find ctx.index_mapping x)
    | Id id -> Some (Hashtbl.find ctx.label_mapping id)
  with Not_found ->
    Error.unbound_index d ~location:idx.info "type" idx;
    None

let resolve_type_index d ctx idx =
  let+@ ty = get_type_info d ctx idx in
  fst ty

let heaptype d ctx (h : Ast.Text.heaptype) : heaptype option =
  match h with
  | Func -> Some Func
  | NoFunc -> Some NoFunc
  | Exn -> Some Exn
  | NoExn -> Some NoExn
  | Extern -> Some Extern
  | NoExtern -> Some NoExtern
  | Any -> Some Any
  | Eq -> Some Eq
  | I31 -> Some I31
  | Struct -> Some Struct
  | Array -> Some Array
  | None_ -> Some None_
  | Type idx ->
      let+@ ty = resolve_type_index d ctx idx in
      Type ty

let reftype d ctx { Ast.Text.nullable; typ } =
  let+@ typ = heaptype d ctx typ in
  { nullable; typ }

let valtype d ctx (ty : Ast.Text.valtype) =
  match ty with
  | I32 -> Some I32
  | I64 -> Some I64
  | F32 -> Some F32
  | F64 -> Some F64
  | V128 -> Some V128
  | Ref r ->
      let+@ ty = reftype d ctx r in
      Ref ty
  | Tuple _ ->
      Error.unsupported_tuple_type d ~location:(Ast.no_loc ()).info;
      None

let array_map_opt f arr =
  let exception Short_circuit in
  try
    let result =
      Array.init (Array.length arr) (fun i ->
          match f arr.(i) with Some v -> v | None -> raise Short_circuit)
    in
    Some result
  with Short_circuit -> None

let array_mapi_opt f arr =
  let exception Short_circuit in
  try
    let result =
      Array.init (Array.length arr) (fun i ->
          match f i arr.(i) with Some v -> v | None -> raise Short_circuit)
    in
    Some result
  with Short_circuit -> None

let functype d ctx { Ast.Text.params; results } =
  let*@ params = array_map_opt (fun (_, ty) -> valtype d ctx ty) params in
  let+@ results = array_map_opt (fun ty -> valtype d ctx ty) results in
  { params; results }

let storagetype d ctx (ty : Ast.Text.storagetype) =
  match ty with
  | Value ty ->
      let+@ ty = valtype d ctx ty in
      Value ty
  | Packed ty -> Some (Packed ty)

let muttype f d ctx { mut; typ } =
  let+@ typ = f d ctx typ in
  { mut; typ }

let fieldtype d ctx ty = muttype storagetype d ctx ty

let tabletype d ctx ({ limits; reftype = typ } : Ast.Text.tabletype) =
  let+@ reftype = reftype d ctx typ in
  { Ast.Binary.limits = limits.desc; reftype }

let globaltype d ctx ty = muttype valtype d ctx ty

let comptype d ctx (ty : Ast.Text.comptype) =
  match ty with
  | Func ty ->
      let+@ ty = functype d ctx ty in
      Func ty
  | Struct fields ->
      let+@ fields = array_map_opt (fun (_, ty) -> fieldtype d ctx ty) fields in
      Struct fields
  | Array field ->
      let+@ field = fieldtype d ctx field in
      Array field

let subtype d ctx current { Ast.Text.typ; supertype; final } =
  let*@ typ = comptype d ctx typ in
  let+@ supertype =
    match supertype with
    | None -> Some None
    | Some idx ->
        let+@ i = resolve_type_index d ctx idx in
        if i <= lnot current then
          Error.unbound_index d ~location:idx.info "type" idx;
        Some i
  in
  { typ; supertype; final }

let rectype d ctx ty = array_mapi_opt (fun i (_, ty) -> subtype d ctx i ty) ty

let signature d ctx { Ast.Text.params; results } =
  let*@ params = array_map_opt (fun (_, ty) -> valtype d ctx ty) params in
  let+@ results = array_map_opt (fun ty -> valtype d ctx ty) results in
  { params; results }

let typeuse d ctx (idx, sign) =
  match (idx, sign) with
  | Some idx, _ -> resolve_type_index d ctx idx
  | _, Some sign ->
      let+@ ty = signature d ctx sign in
      Types.add_rectype ctx.types
        [| { typ = Func ty; supertype = None; final = true } |]
  | None, None -> assert false (* Should not happen *)

let typeuse' d ctx (idx, sign) =
  match (idx, sign) with
  | Some idx, _ -> resolve_type_index d ctx idx
  | _, Some sign ->
      let+@ ty = functype d ctx sign in
      Types.add_rectype ctx.types
        [| { typ = Func ty; supertype = None; final = true } |]
  | None, None -> assert false (* Should not happen *)

let string ctx =
  Types.add_rectype ctx.types
    [|
      {
        typ = Array { mut = true; typ = Packed I8 };
        supertype = None;
        final = true;
      };
    |]

type module_context = {
  diagnostics : Utils.Diagnostic.context;
  types : type_context;
  subtyping_info : Types.subtyping_info;
  functions : int Sequence.t;
  memories : limits Sequence.t;
  tables : Ast.Binary.tabletype Sequence.t;
  globals : globaltype Sequence.t;
  tags : int Sequence.t;
  data : unit Sequence.t;
  elem : reftype Sequence.t;
  exports : (string, unit) Hashtbl.t;
  refs : (int, unit) Hashtbl.t;
}

module IntSet = Set.Make (Int)

type ctx = {
  locals : valtype Sequence.t;
  control_types : (string option * valtype array) list;
  return_types : valtype array;
  modul : module_context;
  mutable initialized_locals : IntSet.t;
}

let lookup_func_type ctx idx =
  let ctx = ctx.modul in
  let*@ ty = resolve_type_index ctx.diagnostics ctx.types idx in
  let def = Types.get_subtype ctx.subtyping_info ty in
  match def.typ with
  | Func f -> Some (ty, f)
  | _ ->
      Error.expected_func_type ctx.diagnostics ~location:idx.info idx;
      None

let lookup_struct_type ctx idx =
  let ctx = ctx.modul in
  let*@ ty, field_map = get_type_info ctx.diagnostics ctx.types idx in
  let def = Types.get_subtype ctx.subtyping_info ty in
  match def.typ with
  | Struct fields -> Some (ty, field_map, fields)
  | _ ->
      Error.expected_struct_type ctx.diagnostics ~location:idx.info idx;
      None

let lookup_array_type ctx idx =
  let ctx = ctx.modul in
  let*@ ty = resolve_type_index ctx.diagnostics ctx.types idx in
  let def = Types.get_subtype ctx.subtyping_info ty in
  match def.typ with
  | Array field -> Some (ty, field)
  | _ ->
      Error.expected_array_type ctx.diagnostics ~location:idx.info idx;
      None

let lookup_tag_type ctx tag =
  let ctx = ctx.modul in
  let+@ ty = Sequence.get ctx.diagnostics ctx.tags tag in
  match (Types.get_subtype ctx.subtyping_info ty).typ with
  | Struct _ | Array _ -> assert false (* Already checked *)
  | Func { params; results } ->
      assert (results = [||]);
      params

type stack =
  | Unreachable
  | Empty
  | Cons of Ast.location option * valtype option * stack

let pop_any ctx loc st =
  match st with
  | Unreachable -> (Unreachable, (None, None))
  | Cons (loc, ty, r) -> (r, (ty, loc))
  | Empty ->
      Error.empty_stack ctx.modul.diagnostics ~location:loc;
      (st, (None, None))

let pop ctx loc ty st =
  match st with
  | Unreachable -> (Unreachable, ())
  | Cons (_, None, r) -> (r, ())
  | Cons (location, Some ty', r) ->
      let ok = Types.val_subtype ctx.modul.subtyping_info ty' ty in
      (if not ok then
         match location with
         | Some location ->
             Error.instruction_type_mismatch ctx.modul.diagnostics ~location ty'
               ty
         | None ->
             Error.type_mismatch ctx.modul.diagnostics ~location:loc ty' ty);
      (r, ())
  | Empty ->
      Error.empty_stack ctx.modul.diagnostics ~location:loc;
      (Unreachable, ())

let push_poly loc ty st = (Cons (Some loc, ty, st), ())
let push loc ty st = (Cons (loc, Some ty, st), ())
let unreachable _ = (Unreachable, ())
let return v st = (st, v)

let ( let* ) e f st =
  let st, v = e st in
  f v st

let ( let*! ) e f = match e with Some v -> f v | None -> unreachable
let ( let*? ) e f = match e with Some v -> f v | None -> ()

let get_local ctx ?(initialize = false) i =
  let+@ l = Sequence.get ctx.modul.diagnostics ctx.locals i in
  let idx = Sequence.get_index ctx.locals i in
  if initialize then
    ctx.initialized_locals <- IntSet.add idx ctx.initialized_locals
  else if not (IntSet.mem idx ctx.initialized_locals) then
    Error.uninitialized_local ctx.modul.diagnostics ~location:i.info i;
  l

let is_nullable ty =
  match ty with
  | None -> true
  | Some (Ref { nullable; _ }) -> nullable
  | _ -> assert false (* Should not happen *)

let is_defaultable ty =
  match ty with
  | I32 | I64 | F32 | F64 | V128 -> true
  | Ref { nullable; _ } -> nullable
  | Tuple _ -> assert false

let number_or_vec ty =
  match ty with
  | I32 | I64 | F32 | F64 | V128 -> true
  | Ref _ | Tuple _ -> false

let int_un_op_type ty (op : Ast.Text.int_un_op) =
  match op with
  | Clz | Ctz | Popcnt | ExtendS _ -> (ty, ty)
  | Trunc (sz, _) | TruncSat (sz, _) ->
      ((match sz with `F32 -> F32 | `F64 -> F64), ty)
  | Reinterpret ->
      ( (match ty with
        | I32 -> F32
        | I64 -> F64
        | _ -> assert false (* Should not happen *)),
        ty )
  | Eqz -> (ty, I32)

let int_bin_op_type ty (op : Ast.Text.int_bin_op) =
  match op with
  | Add | Sub | Mul | Div _ | Rem _ | And | Or | Xor | Shl | Shr _ | Rotl | Rotr
    ->
      ty
  | Eq | Ne | Lt _ | Gt _ | Le _ | Ge _ -> I32

let float_un_op_type ty (op : Ast.Text.float_un_op) =
  match op with
  | Neg | Abs | Ceil | Floor | Trunc | Nearest | Sqrt -> ty
  | Convert (sz, _) -> ( match sz with `I32 -> I32 | `I64 -> I64)
  | Reinterpret -> (
      match ty with
      | F32 -> I32
      | F64 -> I64
      | _ -> assert false (* Should not happen *))

let float_bin_op_type ty (op : Ast.Text.float_bin_op) =
  match op with
  | Add | Sub | Mul | Div | Min | Max | CopySign -> ty
  | Eq | Ne | Lt | Gt | Le | Ge -> I32

let blocktype ctx (ty : Ast.Text.blocktype option) =
  match ty with
  | None -> Some ([||], [||])
  | Some (Typeuse (_, Some { params; results })) ->
      let*@ params =
        array_map_opt
          (fun (_, ty) -> valtype ctx.modul.diagnostics ctx.modul.types ty)
          params
      in
      let+@ results =
        array_map_opt (valtype ctx.modul.diagnostics ctx.modul.types) results
      in
      (params, results)
  | Some (Typeuse (Some idx, None)) ->
      let+@ _, { params; results } = lookup_func_type ctx idx in
      (params, results)
  | Some (Typeuse (None, None)) -> assert false (* Should not happen *)
  | Some (Valtype ty) ->
      let+@ ty = valtype ctx.modul.diagnostics ctx.modul.types ty in
      ([||], [| ty |])

let pop_args ctx loc args =
  Array.fold_right
    (fun ty rem ->
      let* () = rem in
      pop ctx loc ty)
    args (return ())

let push_results results =
  Array.fold_left
    (fun rem ty ->
      let* () = rem in
      push None ty)
    (return ()) results

let rec output_stack ~full f st =
  match st with
  | Empty -> ()
  | Unreachable -> if full then Format.fprintf f "@ unreachable"
  | Cons (_, ty, st) ->
      Format.fprintf f "@ %a%a"
        (Format.pp_print_option
           ~none:(fun f _ -> Format.fprintf f "bot")
           print_valtype)
        ty (output_stack ~full) st

let print_stack st =
  Format.eprintf "@[<2>Stack:%a@]@." (output_stack ~full:true) st;
  (st, ())

let _ = print_stack

let with_empty_stack ctx location f =
  let st, () = f Empty in
  match st with
  | Cons _ ->
      Error.non_empty_stack ctx.diagnostics ~location (fun f () ->
          Format.fprintf f "@[%a@]" (output_stack ~full:false) st)
  | Empty | Unreachable -> ()

let branch_target ctx (idx : Ast.Text.idx) =
  match idx.desc with
  | Num i -> (
      try Some (snd (List.nth ctx.control_types (Uint32.to_int i)))
      with Failure _ ->
        Error.unbound_label ctx.modul.diagnostics ~location:idx.Ast.info idx;
        None)
  | Id id ->
      let rec find l id =
        match l with
        | [] ->
            Error.unbound_label ctx.modul.diagnostics ~location:idx.Ast.info idx;
            None
        | (Some id', res) :: _ when id = id' -> Some res
        | _ :: rem -> find rem id
      in
      find ctx.control_types id

let top_heap_type ctx (t : heaptype) : heaptype =
  match t with
  | Any | Eq | I31 | Struct | Array | None_ -> Any
  | Func | NoFunc -> Func
  | Exn | NoExn -> Exn
  | Extern | NoExtern -> Extern
  | Type ty -> (
      match (Types.get_subtype ctx.modul.subtyping_info ty).typ with
      | Struct _ | Array _ -> Any
      | Func _ -> Func)

let storage_subtype info ty ty' =
  match (ty, ty') with
  | Packed I8, Packed I8 | Packed I16, Packed I16 -> true
  | Value ty, Value ty' -> Types.val_subtype info ty ty'
  | Packed I8, Packed I16
  | Packed I16, Packed I8
  | Packed _, Value _
  | Value _, Packed _ ->
      false

let field_subtype info (ty : fieldtype) (ty' : fieldtype) =
  ty.mut = ty'.mut
  && storage_subtype info ty.typ ty'.typ
  && ((not ty.mut) || storage_subtype info ty'.typ ty.typ)

let diff_ref_type t1 t2 =
  { nullable = t1.nullable && not t2.nullable; typ = t1.typ }

let with_current_stack f st = (st, f st)

let unpack_type (f : fieldtype) =
  match f.typ with Value v -> v | Packed _ -> I32

let rec repeat n f =
  if n = 0 then return ()
  else
    let* () = f in
    repeat (n - 1) f

let address_type_to_valtype = function `I32 -> I32 | `I64 -> I64

(* Constants for max offsets *)

let max_offset_i32_exclusive = Uint64.of_string "0x1_0000_0000" (* 2^32 *)
let max_align = Uint64.of_int 16

let check_memarg ctx location limits sz { Ast.Text.offset; align } =
  if limits.address_type = `I32 then
    if Uint64.compare offset max_offset_i32_exclusive >= 0 then
      Error.memory_offset_too_large ctx.modul.diagnostics ~location
        max_offset_i32_exclusive;
  let natural_alignment =
    match sz with
    | `I8 -> 1
    | `I16 -> 2
    | `I32 | `F32 -> 4
    | `I64 | `F64 -> 8
    | `V128 -> 16
  in
  if
    Uint64.compare align max_align > 0
    || Uint64.to_int align > natural_alignment
  then
    Error.memory_align_too_large ctx.modul.diagnostics ~location
      natural_alignment
  else
    match Uint64.to_int align with
    | 1 | 2 | 4 | 8 | 16 -> ()
    | _ -> Error.bad_memory_align ctx.modul.diagnostics ~location

let memory_instruction_type_and_size ty =
  match (ty : Ast.Text.num_type) with
  | NumI32 -> (I32, `I32)
  | NumF32 -> (F32, `I32)
  | NumI64 -> (I64, `I64)
  | NumF64 -> (F64, `I64)

let field_has_default (ty : fieldtype) =
  match ty.typ with
  | Packed _ -> true
  | Value ty -> (
      match ty with
      | I32 | I64 | F32 | F64 | V128 -> true
      | Ref { nullable; _ } -> nullable
      | Tuple _ -> assert false)

let shape_type (shape : Ast.vec_shape) =
  match shape with
  | I8x16 | I16x8 | I32x4 -> I32
  | I64x2 -> I64
  | F32x4 -> F32
  | F64x2 -> F64

let check_shape_lanes ctx location (shape : Ast.vec_shape) lane =
  let max_lane =
    match shape with
    | I8x16 -> 16
    | I16x8 -> 8
    | I32x4 | F32x4 -> 4
    | I64x2 | F64x2 -> 2
  in
  if lane >= max_lane then
    Error.invalid_lane_index ctx.modul.diagnostics ~location max_lane

let rec instruction ctx (i : _ Ast.Text.instr) =
  if false then Format.eprintf "%a@." print_instr i;
  let loc = i.info in
  match i.desc with
  | Block { label; typ; block = b } ->
      let*! params, results = blocktype ctx typ in
      let* () = pop_args ctx loc params in
      block ctx loc label params results results b;
      push_results results
  | Loop { label; typ; block = b } ->
      let*! params, results = blocktype ctx typ in
      let* () = pop_args ctx loc params in
      block ctx loc label params results params b;
      push_results results
  | If { label; typ; if_block; else_block } ->
      let*! params, results = blocktype ctx typ in
      let* () = pop ctx loc I32 in
      let* () = pop_args ctx loc params in
      block ctx loc label params results results if_block;
      block ctx loc label params results results else_block;
      push_results results
  | TryTable { label; typ; block = b; catches } ->
      let*! params, results = blocktype ctx typ in
      let* () = pop_args ctx loc params in
      block ctx loc label params results results b;
      List.iter
        (fun (catch : Ast.Text.catch) ->
          match catch with
          | Catch (tag, label) ->
              let*? args = lookup_tag_type ctx tag in
              let*? params = branch_target ctx label in
              with_empty_stack ctx.modul loc (*ZZZ*)
                (let* () = push_results args in
                 pop_args ctx loc params)
          | CatchRef (tag, label) ->
              let*? args = lookup_tag_type ctx tag in
              let*? params = branch_target ctx label in
              with_empty_stack ctx.modul loc (*ZZZ*)
                (let* () = push_results args in
                 let* () = push None (Ref { nullable = false; typ = Exn }) in
                 pop_args ctx loc params)
          | CatchAll label ->
              Option.iter
                (fun params ->
                  with_empty_stack ctx.modul loc
                    (*ZZZ*) (pop_args ctx loc params))
                (branch_target ctx label)
          | CatchAllRef label ->
              Option.iter
                (fun params ->
                  with_empty_stack ctx.modul loc (*ZZZ*)
                    (let* () =
                       push None (Ref { nullable = false; typ = Exn })
                     in
                     pop_args ctx loc params))
                (branch_target ctx label))
        catches;
      push_results results
  | Try { label; typ; block = b; catches; catch_all } ->
      let*! params, results = blocktype ctx typ in
      let* () = pop_args ctx loc params in
      block ctx loc label params results results b;
      List.iter
        (fun (tag, b) ->
          let*? params' = lookup_tag_type ctx tag in
          block ctx loc label params' results results b)
        catches;
      Option.iter
        (fun b -> block ctx loc label params results results b)
        catch_all;
      push_results results
  | Unreachable -> unreachable
  | Nop -> return ()
  | Throw idx ->
      let*! params = lookup_tag_type ctx idx in
      let* () = pop_args ctx loc params in
      unreachable
  | ThrowRef ->
      let* () = pop ctx loc (Ref { nullable = true; typ = Exn }) in
      unreachable
  | Br idx ->
      let*! params = branch_target ctx idx in
      let* () = pop_args ctx loc params in
      unreachable
  | Br_if idx ->
      let* () = pop ctx loc I32 in
      let*! params = branch_target ctx idx in
      let* () = pop_args ctx loc params in
      push_results params
  | Br_table (lst, idx) ->
      let* () = pop ctx loc I32 in
      let*! params = branch_target ctx idx in
      let len = Array.length params in
      let* () =
        with_current_stack (fun st ->
            List.iter
              (fun idx' ->
                let*? params = branch_target ctx idx' in
                let len' = Array.length params in
                if len <> len' then
                  Error.branch_parameter_count_mismatch ctx.modul.diagnostics
                    ~location:loc idx len idx' len'
                else ignore (pop_args ctx loc params st))
              (idx :: lst))
      in
      unreachable
  | Br_on_null idx -> (
      let* ty, loc' = pop_any ctx loc in
      match ty with
      | None -> return ()
      | Some (Ref { nullable = _; typ }) ->
          let*! params = branch_target ctx idx in
          let* () = pop_args ctx loc params in
          let* () = push_results params in
          push (Some loc) (Ref { nullable = false; typ })
      | Some ty ->
          Error.expected_ref_type ctx.modul.diagnostics ~location:loc
            ~src_loc:loc' ty;
          unreachable)
  | Br_on_non_null idx -> (
      let* ty, loc' = pop_any ctx loc in
      match ty with
      | None -> return ()
      | Some (Ref { nullable = _; typ }) ->
          let* () = push None (Ref { nullable = false; typ }) in
          let*! params = branch_target ctx idx in
          let* () = pop_args ctx loc params in
          let* () = push_results params in
          let* _ = pop_any ctx loc in
          return ()
      | Some ty ->
          Error.expected_ref_type ctx.modul.diagnostics ~location:loc
            ~src_loc:loc' ty;
          unreachable)
  | Br_on_cast (idx, ty1, ty2) ->
      let*! ty1 = reftype ctx.modul.diagnostics ctx.modul.types ty1 in
      let*! ty2 = reftype ctx.modul.diagnostics ctx.modul.types ty2 in
      (* ZZZ Relaxed condition *)
      if not (Types.val_subtype ctx.modul.subtyping_info (Ref ty2) (Ref ty1))
      then Error.br_cast_type_mismatch ctx.modul.diagnostics ~location:loc;
      let* () = pop ctx loc (Ref ty1) in
      let* () = push None (Ref ty2) in
      let*! params = branch_target ctx idx in
      let* () = pop_args ctx loc params in
      let* () = push_results params in
      let* _ = pop_any ctx loc in
      push (Some loc) (Ref (diff_ref_type ty1 ty2))
  | Br_on_cast_fail (idx, ty1, ty2) ->
      let*! ty1 = reftype ctx.modul.diagnostics ctx.modul.types ty1 in
      let*! ty2 = reftype ctx.modul.diagnostics ctx.modul.types ty2 in
      if not (Types.val_subtype ctx.modul.subtyping_info (Ref ty2) (Ref ty1))
      then Error.br_cast_type_mismatch ctx.modul.diagnostics ~location:loc;
      let* () = pop ctx loc (Ref ty1) in
      let* () = push None (Ref (diff_ref_type ty1 ty2)) in
      let*! params = branch_target ctx idx in
      let* () = pop_args ctx loc params in
      let* () = push_results params in
      let* _ = pop_any ctx loc in
      push (Some loc) (Ref ty2)
  | Return ->
      let* () = pop_args ctx loc ctx.return_types in
      unreachable
  | Call idx -> (
      let*! ty = Sequence.get ctx.modul.diagnostics ctx.modul.functions idx in
      match (Types.get_subtype ctx.modul.subtyping_info ty).typ with
      | Struct _ | Array _ ->
          Error.expected_func_type ctx.modul.diagnostics ~location:loc idx;
          unreachable
      | Func { params; results } ->
          let* () = pop_args ctx loc params in
          push_results results)
  | CallRef idx ->
      let*! type_idx, { params; results } = lookup_func_type ctx idx in
      let* () = pop ctx loc (Ref { nullable = true; typ = Type type_idx }) in
      let* () = pop_args ctx loc params in
      push_results results
  | CallIndirect (idx, tu) -> (
      let*! typ = Sequence.get ctx.modul.diagnostics ctx.modul.tables idx in
      let*! ty = typeuse' ctx.modul.diagnostics ctx.modul.types tu in
      if
        not
          (Types.val_subtype ctx.modul.subtyping_info (Ref typ.reftype)
             (Ref { nullable = true; typ = Func }))
      then
        Error.table_type_mismatch ctx.modul.diagnostics ~location:loc idx
          (Ref typ.reftype);
      match (Types.get_subtype ctx.modul.subtyping_info ty).typ with
      | Struct _ | Array _ ->
          Error.expected_func_type ctx.modul.diagnostics ~location:loc idx;
          unreachable
      | Func { params; results } ->
          let* () =
            pop ctx loc (address_type_to_valtype typ.limits.address_type)
          in
          let* () = pop_args ctx loc params in
          push_results results)
  | ReturnCall idx -> (
      let*! ty = Sequence.get ctx.modul.diagnostics ctx.modul.functions idx in
      match (Types.get_subtype ctx.modul.subtyping_info ty).typ with
      | Struct _ | Array _ ->
          Error.expected_func_type ctx.modul.diagnostics ~location:loc idx;
          unreachable
      | Func { params; results } ->
          let* () = pop_args ctx loc params in
          with_empty_stack ctx.modul loc (*ZZZ*)
            (let* () = push_results results in
             pop_args ctx loc ctx.return_types);
          unreachable)
  | ReturnCallRef idx ->
      let*! type_idx, { params; results } = lookup_func_type ctx idx in
      let* () = pop ctx loc (Ref { nullable = true; typ = Type type_idx }) in
      let* () = pop_args ctx loc params in
      with_empty_stack ctx.modul loc (*ZZZ*)
        (let* () = push_results results in
         pop_args ctx loc ctx.return_types);
      unreachable
  | ReturnCallIndirect (idx, tu) -> (
      let*! typ = Sequence.get ctx.modul.diagnostics ctx.modul.tables idx in
      let*! ty = typeuse' ctx.modul.diagnostics ctx.modul.types tu in
      if
        not
          (Types.val_subtype ctx.modul.subtyping_info (Ref typ.reftype)
             (Ref { nullable = true; typ = Func }))
      then
        Error.table_type_mismatch ctx.modul.diagnostics ~location:loc idx
          (Ref typ.reftype);
      match (Types.get_subtype ctx.modul.subtyping_info ty).typ with
      | Struct _ | Array _ ->
          Error.expected_func_type ctx.modul.diagnostics ~location:loc idx;
          unreachable
      | Func { params; results } ->
          let* () =
            pop ctx loc (address_type_to_valtype typ.limits.address_type)
          in
          let* () = pop_args ctx loc params in
          with_empty_stack ctx.modul loc (*ZZZ*)
            (let* () = push_results results in
             pop_args ctx loc ctx.return_types);
          unreachable)
  | Drop ->
      let* _ = pop_any ctx loc in
      return ()
  | Select None -> (
      let* () = pop ctx loc I32 in
      let* ty1, _ = pop_any ctx loc in
      let* ty2, _ = pop_any ctx loc in
      match (ty1, ty2) with
      | None, None -> push_poly loc None
      | Some ty1, Some ty2 ->
          (*ZZZ*)
          if not (number_or_vec ty1) then
            Format.eprintf "%a@." print_valtype ty1;
          if not (number_or_vec ty2) then
            Format.eprintf "%a@." print_valtype ty2;
          (*ZZZ*)
          assert (number_or_vec ty1);
          assert (number_or_vec ty2);
          if ty1 <> ty2 then
            Error.select_type_mismatch ctx.modul.diagnostics ~location:loc ty1
              ty2;
          push (Some loc) ty1
      | Some ty, None | None, Some ty ->
          assert (number_or_vec ty);
          (*ZZZ*)
          push (Some loc) ty)
  | Select (Some lst) -> (
      match lst with
      | [ typ ] ->
          let*! typ = valtype ctx.modul.diagnostics ctx.modul.types typ in
          let* () = pop ctx loc I32 in
          let* () = pop ctx loc typ in
          let* () = pop ctx loc typ in
          push (Some loc) typ
      | _ -> (*ZZZ*) assert false)
  | LocalGet i ->
      let*! ty = get_local ctx i in
      push (Some loc) ty
  | LocalSet i ->
      let*! ty = get_local ~initialize:true ctx i in
      pop ctx loc ty
  | LocalTee i ->
      let*! ty = get_local ~initialize:true ctx i in
      let* () = pop ctx loc ty in
      push (Some loc) ty
  | GlobalGet idx ->
      let*! ty = Sequence.get ctx.modul.diagnostics ctx.modul.globals idx in
      push (Some loc) ty.typ
  | GlobalSet idx ->
      let*! ty = Sequence.get ctx.modul.diagnostics ctx.modul.globals idx in
      if not ty.mut then
        Error.immutable_global ctx.modul.diagnostics ~location:loc idx;
      pop ctx loc ty.typ
  | Load (idx, memarg, ty) ->
      let*! limits =
        Sequence.get ctx.modul.diagnostics ctx.modul.memories idx
      in
      let ty, sz = memory_instruction_type_and_size ty in
      check_memarg ctx loc limits sz memarg;
      let* () = pop ctx loc (address_type_to_valtype limits.address_type) in
      push (Some loc) ty
  | LoadS (idx, memarg, ty, sz, _) ->
      let*! limits =
        Sequence.get ctx.modul.diagnostics ctx.modul.memories idx
      in
      let ty = match ty with `I32 -> I32 | `I64 -> I64 in
      check_memarg ctx loc limits
        (sz :> [ `I8 | `I16 | `I32 | `I64 | `V128 ])
        memarg;
      let* () = pop ctx loc (address_type_to_valtype limits.address_type) in
      push (Some loc) ty
  | Store (idx, memarg, ty) ->
      let*! limits =
        Sequence.get ctx.modul.diagnostics ctx.modul.memories idx
      in
      let ty, sz = memory_instruction_type_and_size ty in
      check_memarg ctx loc limits sz memarg;
      let* () = pop ctx loc ty in
      let* () = pop ctx loc (address_type_to_valtype limits.address_type) in
      return ()
  | StoreS (idx, memarg, ty, sz) ->
      let*! limits =
        Sequence.get ctx.modul.diagnostics ctx.modul.memories idx
      in
      let ty = match ty with `I32 -> I32 | `I64 -> I64 in
      check_memarg ctx loc limits
        (sz :> [ `I8 | `I16 | `I32 | `I64 | `V128 ])
        memarg;
      let* () = pop ctx loc ty in
      pop ctx loc (address_type_to_valtype limits.address_type)
  | MemorySize idx ->
      let*! limits =
        Sequence.get ctx.modul.diagnostics ctx.modul.memories idx
      in
      push (Some loc) (address_type_to_valtype limits.address_type)
  | MemoryGrow idx ->
      let*! limits =
        Sequence.get ctx.modul.diagnostics ctx.modul.memories idx
      in
      let addr_ty = address_type_to_valtype limits.address_type in
      let* () = pop ctx loc addr_ty in
      push (Some loc) addr_ty
  | MemoryFill idx ->
      let*! limits =
        Sequence.get ctx.modul.diagnostics ctx.modul.memories idx
      in
      let addr_ty = address_type_to_valtype limits.address_type in
      let* () = pop ctx loc addr_ty in
      let* () = pop ctx loc I32 in
      pop ctx loc addr_ty
  | MemoryCopy (idx, idx') ->
      let*! limits =
        Sequence.get ctx.modul.diagnostics ctx.modul.memories idx
      in
      let*! limits' =
        Sequence.get ctx.modul.diagnostics ctx.modul.memories idx'
      in
      let address_type =
        match (limits.address_type, limits'.address_type) with
        | `I32, _ | _, `I32 -> `I32
        | `I64, `I64 -> `I64
      in
      let addr_ty = address_type_to_valtype limits.address_type in
      let addr_ty' = address_type_to_valtype limits.address_type in
      let addr_ty'' = address_type_to_valtype address_type in
      let* () = pop ctx loc addr_ty'' in
      let* () = pop ctx loc addr_ty' in
      pop ctx loc addr_ty
  | MemoryInit (idx, idx') ->
      let*! limits =
        Sequence.get ctx.modul.diagnostics ctx.modul.memories idx
      in
      ignore (Sequence.get ctx.modul.diagnostics ctx.modul.data idx');
      let addr_ty = address_type_to_valtype limits.address_type in
      let* () = pop ctx loc I32 in
      let* () = pop ctx loc I32 in
      pop ctx loc addr_ty
  | DataDrop idx ->
      ignore (Sequence.get ctx.modul.diagnostics ctx.modul.data idx);
      return ()
  | VecBinOp _ ->
      let* () = pop ctx loc V128 in
      let* () = pop ctx loc V128 in
      push (Some loc) V128
  | VecConst _ -> push (Some loc) V128
  | VecUnOp _ ->
      let* () = pop ctx loc V128 in
      push (Some loc) V128
  | VecTest _ ->
      let* () = pop ctx loc V128 in
      push (Some loc) I32
  | VecShift _ ->
      let* () = pop ctx loc I32 in
      let* () = pop ctx loc V128 in
      push (Some loc) V128
  | VecBitmask _ ->
      let* () = pop ctx loc V128 in
      push (Some loc) I32
  | VecTernOp _ ->
      let* () = pop ctx loc V128 in
      let* () = pop ctx loc V128 in
      let* () = pop ctx loc V128 in
      push (Some loc) V128
  | VecBitselect ->
      let* () = pop ctx loc V128 in
      let* () = pop ctx loc V128 in
      let* () = pop ctx loc V128 in
      push (Some loc) V128
  | VecSplat shape ->
      let* () = pop ctx loc (shape_type shape) in
      push (Some loc) V128
  | VecLoad (idx, sz, memarg) ->
      let*! limits =
        Sequence.get ctx.modul.diagnostics ctx.modul.memories idx
      in
      check_memarg ctx loc limits
        (match sz with
        | Load128 -> `V128
        | Load8x8S | Load8x8U | Load16x4S | Load16x4U | Load32x2S | Load32x2U
        | Load64Zero ->
            `I64
        | Load32Zero -> `I32)
        memarg;
      let* () = pop ctx loc (address_type_to_valtype limits.address_type) in
      push (Some loc) V128
  | VecStore (idx, memarg) ->
      let*! limits =
        Sequence.get ctx.modul.diagnostics ctx.modul.memories idx
      in
      check_memarg ctx loc limits `V128 memarg;
      let* () = pop ctx loc V128 in
      let* () = pop ctx loc (address_type_to_valtype limits.address_type) in
      return ()
  | VecLoadLane (idx, op, mem, lane) ->
      let*! limits =
        Sequence.get ctx.modul.diagnostics ctx.modul.memories idx
      in
      check_memarg ctx loc limits
        (op :> [ `I8 | `I16 | `I32 | `I64 | `F32 | `F64 | `V128 ])
        mem;
      let sz = match op with `I8 -> 1 | `I16 -> 2 | `I32 -> 4 | `I64 -> 8 in
      if lane >= 16 / sz then
        Error.invalid_lane_index ctx.modul.diagnostics ~location:loc (16 / sz);
      let* () = pop ctx loc V128 in
      let* () = pop ctx loc (address_type_to_valtype limits.address_type) in
      push (Some loc) V128
  | VecStoreLane (idx, op, mem, lane) ->
      let*! limits =
        Sequence.get ctx.modul.diagnostics ctx.modul.memories idx
      in
      check_memarg ctx loc limits
        (op :> [ `I8 | `I16 | `I32 | `I64 | `F32 | `F64 | `V128 ])
        mem;
      let sz = match op with `I8 -> 1 | `I16 -> 2 | `I32 -> 4 | `I64 -> 8 in
      if lane >= 16 / sz then
        Error.invalid_lane_index ctx.modul.diagnostics ~location:loc (16 / sz);
      let* () = pop ctx loc V128 in
      let* () = pop ctx loc (address_type_to_valtype limits.address_type) in
      return ()
  | VecLoadSplat (idx, op, mem) ->
      let*! limits =
        Sequence.get ctx.modul.diagnostics ctx.modul.memories idx
      in
      check_memarg ctx loc limits
        (op :> [ `I8 | `I16 | `I32 | `I64 | `F32 | `F64 | `V128 ])
        mem;
      let* () = pop ctx loc (address_type_to_valtype limits.address_type) in
      push (Some loc) V128
  | VecExtract (shape, _, lane) ->
      check_shape_lanes ctx loc shape lane;
      let* () = pop ctx loc V128 in
      push (Some loc) (shape_type shape)
  | VecReplace (shape, lane) ->
      check_shape_lanes ctx loc shape lane;
      let* () = pop ctx loc (shape_type shape) in
      let* () = pop ctx loc V128 in
      push (Some loc) V128
  | VecShuffle lanes ->
      (*ZZZ*)
      assert (String.for_all (fun l -> Char.code l < 32) lanes);
      let* () = pop ctx loc V128 in
      let* () = pop ctx loc V128 in
      push (Some loc) V128
  | TableGet idx ->
      let*! typ = Sequence.get ctx.modul.diagnostics ctx.modul.tables idx in
      let addr_ty = address_type_to_valtype typ.limits.address_type in
      let* () = pop ctx loc addr_ty in
      push (Some loc) (Ref typ.reftype)
  | TableSet idx ->
      let*! typ = Sequence.get ctx.modul.diagnostics ctx.modul.tables idx in
      let addr_ty = address_type_to_valtype typ.limits.address_type in
      let* () = pop ctx loc (Ref typ.reftype) in
      pop ctx loc addr_ty
  | TableSize idx ->
      let*! typ = Sequence.get ctx.modul.diagnostics ctx.modul.tables idx in
      push (Some loc) (address_type_to_valtype typ.limits.address_type)
  | TableGrow idx ->
      let*! typ = Sequence.get ctx.modul.diagnostics ctx.modul.tables idx in
      let addr_ty = address_type_to_valtype typ.limits.address_type in
      let* () = pop ctx loc addr_ty in
      let* () = pop ctx loc (Ref typ.reftype) in
      push (Some loc) addr_ty
  | TableFill idx ->
      let*! typ = Sequence.get ctx.modul.diagnostics ctx.modul.tables idx in
      let addr_ty = address_type_to_valtype typ.limits.address_type in
      let* () = pop ctx loc addr_ty in
      let* () = pop ctx loc (Ref typ.reftype) in
      pop ctx loc addr_ty
  | TableCopy (idx, idx') ->
      let*! ty = Sequence.get ctx.modul.diagnostics ctx.modul.tables idx in
      let*! ty' = Sequence.get ctx.modul.diagnostics ctx.modul.tables idx' in
      (*ZZZ*)
      assert (
        Types.val_subtype ctx.modul.subtyping_info (Ref ty'.reftype)
          (Ref ty.reftype));
      let address_type =
        match (ty.limits.address_type, ty'.limits.address_type) with
        | `I32, _ | _, `I32 -> `I32
        | `I64, `I64 -> `I64
      in
      let addr_ty = address_type_to_valtype ty.limits.address_type in
      let addr_ty' = address_type_to_valtype ty'.limits.address_type in
      let addr_ty'' = address_type_to_valtype address_type in
      let* () = pop ctx loc addr_ty'' in
      let* () = pop ctx loc addr_ty' in
      pop ctx loc addr_ty
  | TableInit (idx, idx') ->
      let*! tabletype =
        Sequence.get ctx.modul.diagnostics ctx.modul.tables idx
      in
      let*! typ = Sequence.get ctx.modul.diagnostics ctx.modul.elem idx' in
      (*ZZZ*)
      assert (
        Types.val_subtype ctx.modul.subtyping_info (Ref typ)
          (Ref tabletype.reftype));
      let addr_ty = address_type_to_valtype tabletype.limits.address_type in
      let* () = pop ctx loc I32 in
      let* () = pop ctx loc I32 in
      pop ctx loc addr_ty
  | ElemDrop idx ->
      let*! _ = Sequence.get ctx.modul.diagnostics ctx.modul.elem idx in
      return ()
  | RefNull typ ->
      let*! typ = heaptype ctx.modul.diagnostics ctx.modul.types typ in
      push (Some loc) (Ref { nullable = true; typ })
  | RefFunc idx ->
      let*! i = Sequence.get ctx.modul.diagnostics ctx.modul.functions idx in
      (*ZZZ*)
      assert ((not !validate_refs) || Hashtbl.mem ctx.modul.refs i);
      push (Some loc) (Ref { nullable = false; typ = Type i })
  | RefIsNull -> (
      let* ty, loc' = pop_any ctx loc in
      match ty with
      | None -> return ()
      | Some (Ref _) -> push (Some loc) I32
      | Some ty ->
          Error.expected_ref_type ctx.modul.diagnostics ~location:loc
            ~src_loc:loc' ty;
          unreachable)
  | RefAsNonNull -> (
      let* ty, loc' = pop_any ctx loc in
      match ty with
      | None -> return ()
      | Some (Ref ty) -> push (Some loc) (Ref { ty with nullable = false })
      | Some ty ->
          Error.expected_ref_type ctx.modul.diagnostics ~location:loc
            ~src_loc:loc' ty;
          unreachable)
  | RefEq ->
      let* () = pop ctx loc (Ref { nullable = true; typ = Eq }) in
      let* () = pop ctx loc (Ref { nullable = true; typ = Eq }) in
      push (Some loc) I32
  | RefTest ty ->
      let*! ty = reftype ctx.modul.diagnostics ctx.modul.types ty in
      let* () =
        pop ctx loc (Ref { nullable = true; typ = top_heap_type ctx ty.typ })
      in
      push (Some loc) I32
  | RefCast ty ->
      let*! ty = reftype ctx.modul.diagnostics ctx.modul.types ty in
      let* () =
        pop ctx loc (Ref { nullable = true; typ = top_heap_type ctx ty.typ })
      in
      push (Some loc) (Ref ty)
  | StructNew idx ->
      let*! ty, _, fields = lookup_struct_type ctx idx in
      let* () =
        pop_args ctx loc
          (Array.map
             (fun (f : fieldtype) ->
               match f.typ with Value v -> v | Packed _ -> I32)
             fields)
      in
      push (Some loc) (Ref { nullable = false; typ = Type ty })
  | StructNewDefault idx ->
      let*! ty, _, fields = lookup_struct_type ctx idx in
      (*ZZZ*)
      assert (Array.for_all field_has_default fields);
      push (Some loc) (Ref { nullable = false; typ = Type ty })
  | StructGet (signage, idx, idx') ->
      let*! ty, field_map, fields = lookup_struct_type ctx idx in
      let* () = pop ctx loc (Ref { nullable = true; typ = Type ty }) in
      let n =
        match idx'.desc with
        | Id id -> List.assoc id field_map (*ZZZ*)
        | Num n ->
            let n = Uint32.to_int n in
            assert (n < Array.length fields);
            (*ZZZ*)
            n
      in
      (match fields.(n).typ with
      | Packed _ ->
          if signage = None then
            Error.packed_struct_access ctx.modul.diagnostics ~location:i.info
      | Value _ ->
          if signage <> None then
            Error.unpacked_struct_access ctx.modul.diagnostics ~location:i.info);
      (*ZZZ signage + validate n*)
      ignore signage;
      push (Some loc) (unpack_type fields.(n))
  | StructSet (idx, idx') ->
      let*! ty, field_map, fields = lookup_struct_type ctx idx in
      let n =
        match idx'.desc with
        | Id id -> List.assoc id field_map (*ZZZ*)
        | Num n -> Uint32.to_int n
      in
      (*ZZZ*)
      assert fields.(n).mut;
      let* () = pop ctx loc (unpack_type fields.(n)) in
      pop ctx loc (Ref { nullable = true; typ = Type ty })
  | ArrayNew idx ->
      let*! ty, field = lookup_array_type ctx idx in
      let* () = pop ctx loc I32 in
      let* () = pop ctx loc (unpack_type field) in
      push (Some loc) (Ref { nullable = false; typ = Type ty })
  | ArrayNewDefault idx ->
      let*! ty, field = lookup_array_type ctx idx in
      (*ZZZ*)
      assert (field_has_default field);
      let* () = pop ctx loc I32 in
      push (Some loc) (Ref { nullable = false; typ = Type ty })
  | ArrayNewFixed (idx, n) ->
      let*! ty, field = lookup_array_type ctx idx in
      let* () = repeat (Uint32.to_int n) (pop ctx loc (unpack_type field)) in
      push (Some loc) (Ref { nullable = false; typ = Type ty })
  | ArrayNewData (idx, idx') ->
      let*! ty, field = lookup_array_type ctx idx in
      ignore (Sequence.get ctx.modul.diagnostics ctx.modul.data idx');
      (*ZZZ*)
      assert (
        match field.typ with
        | Packed _ | Value (I32 | I64 | F32 | F64 | V128) -> true
        | Value (Ref _ | Tuple _) -> false);
      let* () = pop ctx loc I32 in
      let* () = pop ctx loc I32 in
      push (Some loc) (Ref { nullable = false; typ = Type ty })
  | ArrayNewElem (idx, idx') ->
      let*! ty, field = lookup_array_type ctx idx in
      let*! ty' = Sequence.get ctx.modul.diagnostics ctx.modul.elem idx' in
      (*ZZZ*)
      assert (
        match field.typ with
        | Packed _ -> false
        | Value ty -> Types.val_subtype ctx.modul.subtyping_info (Ref ty') ty);
      let* () = pop ctx loc I32 in
      let* () = pop ctx loc I32 in
      push (Some loc) (Ref { nullable = false; typ = Type ty })
  | ArrayGet (signage, idx) ->
      let*! ty, field = lookup_array_type ctx idx in
      (match field.typ with
      | Packed _ ->
          if signage = None then
            Error.packed_array_access ctx.modul.diagnostics ~location:i.info
      | Value _ ->
          if signage <> None then
            Error.unpacked_array_access ctx.modul.diagnostics ~location:i.info);
      let* () = pop ctx loc I32 in
      let* () = pop ctx loc (Ref { nullable = true; typ = Type ty }) in
      push (Some loc) (unpack_type field)
  | ArraySet idx ->
      let*! ty, field = lookup_array_type ctx idx in
      (*ZZZ*)
      assert field.mut;
      let* () = pop ctx loc (unpack_type field) in
      let* () = pop ctx loc I32 in
      pop ctx loc (Ref { nullable = true; typ = Type ty })
  | ArrayLen ->
      let* () = pop ctx loc (Ref { nullable = true; typ = Array }) in
      push (Some loc) I32
  | ArrayFill idx ->
      let*! ty, field = lookup_array_type ctx idx in
      (*ZZZ*)
      assert field.mut;
      let* () = pop ctx loc I32 in
      let* () = pop ctx loc (unpack_type field) in
      let* () = pop ctx loc I32 in
      pop ctx loc (Ref { nullable = true; typ = Type ty })
  | ArrayCopy (idx1, idx2) ->
      let*! ty1, field1 = lookup_array_type ctx idx1 in
      let*! ty2, field2 = lookup_array_type ctx idx2 in
      (*ZZZ*)
      assert field1.mut;
      assert (storage_subtype ctx.modul.subtyping_info field1.typ field2.typ);
      let* () = pop ctx loc I32 in
      let* () = pop ctx loc I32 in
      let* () = pop ctx loc (Ref { nullable = true; typ = Type ty2 }) in
      let* () = pop ctx loc I32 in
      pop ctx loc (Ref { nullable = true; typ = Type ty1 })
  | ArrayInitData (idx, idx') ->
      let*! ty, field = lookup_array_type ctx idx in
      ignore (Sequence.get ctx.modul.diagnostics ctx.modul.data idx');
      (*ZZZ*)
      assert field.mut;
      assert (
        match field.typ with
        | Packed _ | Value (I32 | I64 | F32 | F64 | V128) -> true
        | Value (Ref _ | Tuple _) -> false);
      let* () = pop ctx loc I32 in
      let* () = pop ctx loc I32 in
      let* () = pop ctx loc I32 in
      pop ctx loc (Ref { nullable = true; typ = Type ty })
  | ArrayInitElem (idx, idx') ->
      let*! ty, field = lookup_array_type ctx idx in
      let*! ty' = Sequence.get ctx.modul.diagnostics ctx.modul.elem idx' in
      (*ZZZ*)
      assert field.mut;
      assert (
        match field.typ with
        | Packed _ -> false
        | Value ty -> Types.val_subtype ctx.modul.subtyping_info (Ref ty') ty);
      let* () = pop ctx loc I32 in
      let* () = pop ctx loc I32 in
      let* () = pop ctx loc I32 in
      pop ctx loc (Ref { nullable = true; typ = Type ty })
  | RefI31 ->
      let* () = pop ctx loc I32 in
      push (Some loc) (Ref { nullable = false; typ = I31 })
  | I31Get _ ->
      let* () = pop ctx loc (Ref { nullable = true; typ = I31 }) in
      push (Some loc) I32
  | Const (I32 _) -> push (Some loc) I32
  | Const (I64 _) -> push (Some loc) I64
  | Const (F32 _) -> push (Some loc) F32
  | Const (F64 _) -> push (Some loc) F64
  | UnOp (I32 op) ->
      let expected, returned = int_un_op_type I32 op in
      let* () = pop ctx loc expected in
      push (Some loc) returned
  | UnOp (I64 op) ->
      let expected, returned = int_un_op_type I64 op in
      let* () = pop ctx loc expected in
      push (Some loc) returned
  | UnOp (F32 op) ->
      let expected = float_un_op_type F32 op in
      let* () = pop ctx loc expected in
      push (Some loc) F32
  | UnOp (F64 op) ->
      let expected = float_un_op_type F64 op in
      let* () = pop ctx loc expected in
      push (Some loc) F64
  | BinOp (I32 op) ->
      let* () = pop ctx loc I32 in
      let* () = pop ctx loc I32 in
      push (Some loc) (int_bin_op_type I32 op)
  | BinOp (I64 op) ->
      let* () = pop ctx loc I64 in
      let* () = pop ctx loc I64 in
      push (Some loc) (int_bin_op_type I64 op)
  | BinOp (F32 op) ->
      let* () = pop ctx loc F32 in
      let* () = pop ctx loc F32 in
      push (Some loc) (float_bin_op_type F32 op)
  | BinOp (F64 op) ->
      let* () = pop ctx loc F64 in
      let* () = pop ctx loc F64 in
      push (Some loc) (float_bin_op_type F64 op)
  | I32WrapI64 ->
      let* () = pop ctx loc I64 in
      push (Some loc) I32
  | I64ExtendI32 _ ->
      let* () = pop ctx loc I32 in
      push (Some loc) I64
  | F32DemoteF64 ->
      let* () = pop ctx loc F64 in
      push (Some loc) F32
  | F64PromoteF32 ->
      let* () = pop ctx loc F32 in
      push (Some loc) F64
  | ExternConvertAny ->
      let* ty, _ = pop_any ctx loc in
      Option.iter
        (fun ty ->
          (*ZZZ*)
          assert (
            Types.val_subtype ctx.modul.subtyping_info ty
              (Ref { nullable = true; typ = Any })))
        ty;
      push (Some loc) (Ref { nullable = is_nullable ty; typ = Extern })
  | AnyConvertExtern ->
      let* ty, _ = pop_any ctx loc in
      Option.iter
        (fun ty ->
          (*ZZZ*)
          assert (
            Types.val_subtype ctx.modul.subtyping_info ty
              (Ref { nullable = true; typ = Extern })))
        ty;
      push (Some loc) (Ref { nullable = is_nullable ty; typ = Any })
  | Folded (i, l) ->
      let* () = instructions ctx l in
      instruction ctx i
  | TupleMake _ -> return ()
  | Pop ty ->
      let*! ty = valtype ctx.modul.diagnostics ctx.modul.types ty in
      let* () = pop ctx loc ty in
      push (Some loc) ty
  (*
    (* Binaryen extensions *)
    | Pop of X.valtype
    | TupleMake of Int32.t
    | TupleExtract of Int32.t * Int32.t
*)
  | String _ ->
      let ty = Ref { nullable = false; typ = Type (string ctx.modul.types) } in
      push (Some loc) ty
  | Char _ -> push (Some loc) I32
  | TupleExtract _ ->
      Format.eprintf "%a@." print_instr i;
      raise Exit

and instructions ctx l =
  match l with
  | [] -> return ()
  | i :: r ->
      let* () = instruction ctx i in
      instructions ctx r

and block ctx loc label params results br_params block =
  with_empty_stack ctx.modul loc (*ZZZ*)
    (let* () = push_results params in
     let* () =
       instructions
         {
           ctx with
           control_types =
             (Option.map (fun l -> l.Ast.desc) label, br_params)
             :: ctx.control_types;
         }
         block
     in
     pop_args ctx loc (*ZZZ More precise loc*) results)

let rec check_constant_instruction ctx (i : _ Ast.Text.instr) =
  match i.desc with
  | GlobalGet idx ->
      let*? ty = Sequence.get ctx.diagnostics ctx.globals idx in
      (*ZZZ*)
      assert (not ty.mut)
  | RefFunc i ->
      let*? ty = Sequence.get ctx.diagnostics ctx.functions i in
      Hashtbl.replace ctx.refs ty ()
  | RefNull _ | StructNew _ | StructNewDefault _ | ArrayNew _
  | ArrayNewDefault _ | ArrayNewFixed _ | RefI31 | Const _
  | BinOp (I32 (Add | Sub | Mul) | I64 (Add | Sub | Mul))
  | ExternConvertAny | AnyConvertExtern | VecConst _ | String _ | Char _ ->
      ()
  | Folded (i, l) ->
      check_constant_instruction ctx i;
      check_constant_instructions ctx l
  | Block _ | Loop _ | If _ | TryTable _ | Try _ | Unreachable | Nop | Throw _
  | ThrowRef | Br _ | Br_if _ | Br_table _ | Br_on_null _ | Br_on_non_null _
  | Br_on_cast _ | Br_on_cast_fail _ | Return | Call _ | CallRef _
  | CallIndirect _ | ReturnCall _ | ReturnCallRef _ | ReturnCallIndirect _
  | Drop | Select _ | LocalGet _ | LocalSet _ | LocalTee _ | GlobalSet _
  | Load _ | LoadS _ | Store _ | StoreS _ | MemorySize _ | MemoryGrow _
  | MemoryFill _ | MemoryCopy _ | MemoryInit _ | DataDrop _ | TableGet _
  | TableSet _ | TableSize _ | TableGrow _ | TableFill _ | TableCopy _
  | TableInit _ | ElemDrop _ | RefIsNull | RefAsNonNull | RefEq | RefTest _
  | RefCast _ | StructGet _ | StructSet _ | ArrayNewData _ | ArrayNewElem _
  | ArrayGet _ | ArraySet _ | ArrayLen | ArrayFill _ | ArrayCopy _
  | ArrayInitData _ | ArrayInitElem _ | I31Get _ | UnOp _
  | BinOp
      ( F32 _ | F64 _
      | I32
          ( Div _ | Rem _ | And | Or | Xor | Shl | Shr _ | Rotl | Rotr | Eq | Ne
          | Lt _ | Gt _ | Le _ | Ge _ )
      | I64
          ( Div _ | Rem _ | And | Or | Xor | Shl | Shr _ | Rotl | Rotr | Eq | Ne
          | Lt _ | Gt _ | Le _ | Ge _ ) )
  | I32WrapI64 | I64ExtendI32 _ | F32DemoteF64 | F64PromoteF32 | Pop _
  | TupleMake _ | TupleExtract _ ->
      Error.constant_expression_required ctx.diagnostics ~location:i.info
  | VecBitselect | VecUnOp _ | VecBinOp _ | VecTest _ | VecShift _
  | VecBitmask _ | VecLoad _ | VecStore _ | VecLoadLane _ | VecStoreLane _
  | VecLoadSplat _ | VecExtract _ | VecReplace _ | VecSplat _ | VecShuffle _
  | VecTernOp _ ->
      Error.constant_expression_required ctx.diagnostics ~location:i.info

and check_constant_instructions ctx l =
  List.iter (fun i -> check_constant_instruction ctx i) l

let constant_expression ctx ty expr =
  check_constant_instructions ctx expr;
  with_empty_stack ctx (Ast.no_loc ()).info (*ZZZ*)
    (let ctx =
       {
         locals = Sequence.make "local";
         control_types = [];
         return_types = [||];
         modul = ctx;
         initialized_locals = IntSet.empty;
       }
     in
     let* () = instructions ctx expr in
     pop ctx (Ast.no_loc ()).info (*ZZZ*) ty)

let add_type d ctx ty =
  Array.iteri
    (fun i (label, _) ->
      Hashtbl.replace ctx.index_mapping
        (Uint32.of_int (ctx.last_index + i))
        (lnot i, []);
      Option.iter
        (fun label ->
          Hashtbl.replace ctx.label_mapping label.Ast.desc (lnot i, []))
        label)
    ty;
  match rectype d ctx ty with
  | None ->
      Array.iteri
        (fun i (label, _) ->
          Hashtbl.remove ctx.index_mapping (Uint32.of_int (ctx.last_index + i));
          Option.iter
            (fun label -> Hashtbl.remove ctx.label_mapping label.Ast.desc)
            label)
        ty
  | Some ty' ->
      let i' = Types.add_rectype ctx.types ty' in
      Array.iteri
        (fun i (label, typ) ->
          let fields =
            match (typ : Ast.Text.subtype).typ with
            | Struct fields ->
                Array.mapi
                  (fun i (id, _) ->
                    match id with
                    | Some id -> Some (id.Ast.desc, i)
                    | None -> None)
                  fields
                |> Array.to_list |> List.filter_map Fun.id
            | _ -> []
          in
          Hashtbl.replace ctx.index_mapping
            (Uint32.of_int (ctx.last_index + i))
            (i' + i, fields);
          Option.iter
            (fun label ->
              Hashtbl.replace ctx.label_mapping label.Ast.desc (i' + i, fields))
            label)
        ty;
      ctx.last_index <- ctx.last_index + Array.length ty

let register_exports ctx lst =
  List.iter
    (fun (name : Ast.Text.name) ->
      if Hashtbl.mem ctx.exports name.desc then
        Error.duplicated_export ctx.diagnostics ~location:name.info name
      else Hashtbl.add ctx.exports name.desc ())
    lst

let limits ctx kind { Ast.desc = { mi; ma; address_type }; info = location }
    max_fn =
  let max = max_fn address_type in
  match ma with
  | None ->
      if Uint64.compare mi max > 0 then
        Error.limit_too_large ctx.diagnostics ~location kind max
  | Some ma ->
      if Uint64.compare mi ma > 0 then
        Error.limit_mismatch ctx.diagnostics ~location kind;
      if Uint64.compare ma max > 0 then
        Error.limit_too_large ctx.diagnostics ~location kind max

let max_memory_size = function
  | `I32 -> Uint64.of_int 65536
  | `I64 -> Uint64.of_string "0x1_0000_0000_0000"

let max_table_size = function
  | `I32 -> Uint64.of_string "0xffff_ffff"
  | `I64 -> Uint64.of_string "0xffff_ffff_ffff_ffff"

let rec register_typeuses d ctx l =
  List.iter (fun i -> register_typeuses' d ctx i) l

and register_typeuses' d ctx (i : _ Ast.Text.instr) =
  match i.desc with
  | Block { typ; _ }
  | Loop { typ; _ }
  | If { typ; _ }
  | TryTable { typ; _ }
  | Try { typ; _ } -> (
      match typ with
      | Some (Typeuse use) -> ignore (typeuse' d ctx use)
      | Some (Valtype _) | None -> ())
  | CallIndirect (_, use) | ReturnCallIndirect (_, use) ->
      ignore (typeuse' d ctx use)
  | String _ -> ignore (string ctx)
  | Folded (i, l) ->
      register_typeuses' d ctx i;
      register_typeuses d ctx l
  | Unreachable | Nop | Throw _ | ThrowRef | Br _ | Br_if _ | Br_table _
  | Br_on_null _ | Br_on_non_null _ | Br_on_cast _ | Br_on_cast_fail _ | Return
  | Call _ | CallRef _ | ReturnCall _ | ReturnCallRef _ | Drop | Select _
  | LocalGet _ | LocalSet _ | LocalTee _ | GlobalGet _ | GlobalSet _ | Load _
  | LoadS _ | Store _ | StoreS _ | MemorySize _ | MemoryGrow _ | MemoryFill _
  | MemoryCopy _ | MemoryInit _ | DataDrop _ | TableGet _ | TableSet _
  | TableSize _ | TableGrow _ | TableFill _ | TableCopy _ | TableInit _
  | ElemDrop _ | RefNull _ | RefFunc _ | RefIsNull | RefAsNonNull | RefEq
  | RefTest _ | RefCast _ | StructNew _ | StructNewDefault _ | StructGet _
  | StructSet _ | ArrayNew _ | ArrayNewDefault _ | ArrayNewFixed _
  | ArrayNewData _ | ArrayNewElem _ | ArrayGet _ | ArraySet _ | ArrayLen
  | ArrayFill _ | ArrayCopy _ | ArrayInitData _ | ArrayInitElem _ | RefI31
  | I31Get _ | Const _ | UnOp _ | BinOp _ | I32WrapI64 | I64ExtendI32 _
  | F32DemoteF64 | F64PromoteF32 | ExternConvertAny | AnyConvertExtern | Pop _
  | TupleMake _ | TupleExtract _ | VecBitselect | VecConst _ | VecUnOp _
  | VecBinOp _ | VecTest _ | VecShift _ | VecBitmask _ | VecLoad _ | VecStore _
  | VecLoadLane _ | VecStoreLane _ | VecLoadSplat _ | VecExtract _
  | VecReplace _ | VecSplat _ | VecShuffle _ | VecTernOp _ | Char _ ->
      ()

let build_initial_env ctx fields =
  List.iter
    (fun (field : (_ Ast.Text.modulefield, _) Ast.annotated) ->
      match field.desc with
      | Import { id; desc; exports; module_ = _; name = _ } -> (
          (* ZZZ Check for non-import fields *)
          register_exports ctx exports;
          match desc with
          | Func tu ->
              ignore
                (let+@ ty = typeuse ctx.diagnostics ctx.types tu in
                 Sequence.register ctx.functions id ty)
          | Memory lim ->
              limits ctx "memory" lim max_memory_size;
              Sequence.register ctx.memories id lim.desc
          | Table typ ->
              limits ctx "table" typ.limits max_table_size;
              let>@ typ = tabletype ctx.diagnostics ctx.types typ in
              Sequence.register ctx.tables id typ
          | Global ty ->
              let>@ ty = globaltype ctx.diagnostics ctx.types ty in
              Sequence.register ctx.globals id ty
          | Tag tu ->
              let>@ ty = typeuse ctx.diagnostics ctx.types tu in
              (*
              (match (Types.get_subtype ctx.subtyping_info ty).typ with
              | Func { results; _ } -> assert (results = [||])
              | Struct _ | Array _ -> assert false (*ZZZ*));
*)
              Sequence.register ctx.tags id ty)
      | Func { id; typ; instrs; _ } ->
          let>@ typ = typeuse ctx.diagnostics ctx.types typ in
          Sequence.register ctx.functions id typ;
          register_typeuses ctx.diagnostics ctx.types instrs
      | Tag { id; typ; exports } ->
          let>@ ty = typeuse ctx.diagnostics ctx.types typ in
          (*
          (match (Types.get_subtype ctx.subtyping_info ty).typ with
          | Func { results; _ } -> assert (results = [||])
          | Struct _ | Array _ -> assert false (*ZZZ*));
*)
          register_exports ctx exports;
          Sequence.register ctx.tags id ty
      | _ -> ())
    fields

let check_type_definitions ctx =
  for i = 0 to ctx.types.last_index - 1 do
    let>@ i, _ =
      get_type_info ctx.diagnostics ctx.types
        (Ast.no_loc (Ast.Text.Num (Uint32.of_int i)))
    in
    let ty = Types.get_subtype ctx.subtyping_info i in
    let*? j = ty.supertype in
    let ty' = Types.get_subtype ctx.subtyping_info j in
    (*ZZZ*)
    assert (not ty'.final);
    match (ty.typ, ty'.typ) with
    | Func { params; results }, Func { params = params'; results = results' } ->
        (*ZZZ*)
        assert (Array.length params = Array.length params');
        assert (Array.length results = Array.length results');
        Array.iter2
          (fun p p' -> assert (Types.val_subtype ctx.subtyping_info p' p))
          params params';
        Array.iter2
          (fun r r' -> assert (Types.val_subtype ctx.subtyping_info r r'))
          results results'
    | Struct fields, Struct fields' ->
        assert (Array.length fields' <= Array.length fields);
        for i = 0 to Array.length fields' - 1 do
          assert (field_subtype ctx.subtyping_info fields.(i) fields'.(i))
        done
    | Array field, Array field' ->
        assert (field_subtype ctx.subtyping_info field field')
    | Func _, (Struct _ | Array _)
    | Struct _, (Func _ | Array _)
    | Array _, (Func _ | Struct _) ->
        Error.supertype_mismatch ctx.diagnostics
          ~location:(Ast.no_loc ()).info (*ZZZ*)
  done

let tables_and_memories ctx fields =
  List.iter
    (fun (field : (_ Ast.Text.modulefield, _) Ast.annotated) ->
      match field.desc with
      | Memory { id; limits = lim; init = _; exports } ->
          limits ctx "memory" lim max_memory_size;
          Sequence.register ctx.memories id lim.desc;
          register_exports ctx exports
      | Table { id; typ; init; exports } ->
          limits ctx "table" typ.limits max_table_size;
          let>@ typ = tabletype ctx.diagnostics ctx.types typ in
          (match init with
          | Init_default ->
              if not typ.reftype.nullable then
                Error.non_nullable_table_type ctx.diagnostics
                  ~location:field.info (*ZZZ*)
          | Init_expr e -> constant_expression ctx (Ref typ.reftype) e
          | Init_segment _ -> ());
          Sequence.register ctx.tables id typ;
          register_exports ctx exports
      | _ -> ())
    fields

let globals ctx fields =
  List.iter
    (fun (field : (_ Ast.Text.modulefield, _) Ast.annotated) ->
      match field.desc with
      | Global { id; typ; init; exports } ->
          let>@ typ = globaltype ctx.diagnostics ctx.types typ in
          constant_expression ctx typ.typ init;
          Sequence.register ctx.globals id typ;
          register_exports ctx exports
      | String_global { id; _ } ->
          let typ =
            {
              mut = false;
              typ = Ref { nullable = false; typ = Type (string ctx.types) };
            }
          in
          Sequence.register ctx.globals (Some id) typ
      | _ -> ())
    fields

let segments ctx fields =
  List.iter
    (fun (field : (_ Ast.Text.modulefield, _) Ast.annotated) ->
      match field.desc with
      | Memory { init; _ } ->
          let*? _ = init in
          Sequence.register ctx.data None ()
      | Data { id; init = _; mode } ->
          (match mode with
          | Passive -> ()
          | Active (i, e) ->
              let*? limits = Sequence.get ctx.diagnostics ctx.memories i in
              constant_expression ctx
                (address_type_to_valtype limits.address_type)
                e);
          Sequence.register ctx.data id ()
      | Table { typ; init; _ } -> (
          match init with
          | Init_default | Init_expr _ -> ()
          | Init_segment lst ->
              let>@ typ = reftype ctx.diagnostics ctx.types typ.reftype in
              List.iter (fun e -> constant_expression ctx (Ref typ) e) lst;
              Sequence.register ctx.elem None typ)
      | Elem { id; typ; init; mode } ->
          let>@ typ = reftype ctx.diagnostics ctx.types typ in
          (match mode with
          | Passive | Declare -> ()
          | Active (i, e) ->
              let*? tabletype = Sequence.get ctx.diagnostics ctx.tables i in
              if
                not
                  (Types.val_subtype ctx.subtyping_info (Ref typ)
                     (Ref tabletype.reftype))
              then failwith "type mismatch";
              constant_expression ctx
                (address_type_to_valtype tabletype.limits.address_type)
                e);
          List.iter (fun e -> constant_expression ctx (Ref typ) e) init;
          Sequence.register ctx.elem id typ
      | _ -> ())
    fields

let functions ctx fields =
  List.iter
    (fun (field : (_ Ast.Text.modulefield, _) Ast.annotated) ->
      match field.desc with
      | Func { id = _; typ; locals = locs; instrs; exports } ->
          let>@ func_typ =
            let+@ typ = typeuse ctx.diagnostics ctx.types typ in
            match (Types.get_subtype ctx.subtyping_info typ).typ with
            | Func typ -> typ
            | _ -> assert false (*ZZZ*)
          in
          let return_types = func_typ.results in
          let locals = Sequence.make "local" in
          let initialized_locals = ref IntSet.empty in
          let i = ref 0 in
          (match typ with
          | _, Some { params; _ } ->
              Array.iter
                (fun (id, typ) ->
                  initialized_locals := IntSet.add !i !initialized_locals;
                  incr i;
                  Sequence.register locals id
                    (match valtype ctx.diagnostics ctx.types typ with
                    | None ->
                        (* Dummy value *) Ref { nullable = false; typ = None_ }
                    | Some typ -> typ))
                params
          | _ ->
              Array.iter
                (fun typ ->
                  initialized_locals := IntSet.add !i !initialized_locals;
                  incr i;
                  Sequence.register locals None typ)
                func_typ.params);
          List.iter
            (fun (id, typ) ->
              let typ =
                match valtype ctx.diagnostics ctx.types typ with
                | None -> (* Dummy value *) Ref { nullable = true; typ = Any }
                | Some typ -> typ
              in
              if is_defaultable typ then
                initialized_locals := IntSet.add !i !initialized_locals;
              incr i;
              Sequence.register locals id typ)
            locs;
          with_empty_stack ctx (Ast.no_loc ()).info (*ZZZ*)
            (let ctx =
               {
                 locals;
                 control_types = [ (None, return_types) ];
                 return_types;
                 modul = ctx;
                 initialized_locals = !initialized_locals;
               }
             in
             let* () = instructions ctx instrs in
             pop_args ctx (Ast.no_loc ()).info (*ZZZ*) return_types);
          register_exports ctx exports
      | _ -> ())
    fields

let exports ctx fields =
  List.iter
    (fun (field : (_ Ast.Text.modulefield, _) Ast.annotated) ->
      match field.desc with
      | Export { name; kind; index } -> (
          register_exports ctx [ name ];
          match kind with
          | Func -> ignore (Sequence.get ctx.diagnostics ctx.functions index)
          | Memory -> ignore (Sequence.get ctx.diagnostics ctx.memories index)
          | Table -> ignore (Sequence.get ctx.diagnostics ctx.tables index)
          | Tag -> ignore (Sequence.get ctx.diagnostics ctx.tags index)
          | Global -> ignore (Sequence.get ctx.diagnostics ctx.globals index))
      | _ -> ())
    fields

let start ctx fields =
  List.iter
    (fun (field : (_ Ast.Text.modulefield, _) Ast.annotated) ->
      match field.desc with
      | Start idx -> (
          let*? ty = Sequence.get ctx.diagnostics ctx.functions idx in
          match (Types.get_subtype ctx.subtyping_info ty).typ with
          | Struct _ | Array _ -> assert false (* Should not happen *)
          | Func { params; results } -> assert (params = [||] && results = [||])
          )
      | _ -> ())
    fields

let f diagnostics (_, fields) =
  let type_context =
    {
      types = Types.create ();
      last_index = 0;
      index_mapping = Hashtbl.create 16;
      label_mapping = Hashtbl.create 16;
    }
  in
  List.iter
    (fun (field : (_ Ast.Text.modulefield, _) Ast.annotated) ->
      match field.desc with
      | Types rectype -> add_type diagnostics type_context rectype
      | _ -> ())
    fields;
  let ctx =
    {
      diagnostics;
      types = type_context;
      subtyping_info = Types.subtyping_info type_context.types;
      functions = Sequence.make "function";
      memories = Sequence.make "memory";
      tables = Sequence.make "tables";
      globals = Sequence.make "global";
      tags = Sequence.make "tag";
      data = Sequence.make "data";
      elem = Sequence.make "data";
      exports = Hashtbl.create 16;
      refs = Hashtbl.create 16;
    }
  in
  check_type_definitions ctx;
  build_initial_env ctx fields;
  let ctx =
    { ctx with subtyping_info = Types.subtyping_info type_context.types }
  in
  tables_and_memories ctx fields;
  globals ctx fields;
  segments ctx fields;
  functions ctx fields;
  exports ctx fields;
  start ctx fields

let eq_heaptype h1 h2 =
  let open Ast.Text in
  match (h1, h2) with
  | Type i1, Type i2 -> (
      match (i1.Ast.desc, i2.Ast.desc) with
      | Ast.Text.Num n1, Ast.Text.Num n2 -> n1 = n2
      | Ast.Text.Id s1, Ast.Text.Id s2 -> s1 = s2
      | _ -> false)
  | _, _ -> h1 = h2

let eq_reftype r1 r2 =
  r1.Ast.Text.nullable = r2.Ast.Text.nullable
  && eq_heaptype r1.Ast.Text.typ r2.Ast.Text.typ

let rec eq_valtype v1 v2 =
  let open Ast.Text in
  match (v1, v2) with
  | Ast.Text.Types.I32, Ast.Text.Types.I32
  | Ast.Text.Types.I64, Ast.Text.Types.I64
  | Ast.Text.Types.F32, Ast.Text.Types.F32
  | Ast.Text.Types.F64, Ast.Text.Types.F64
  | Ast.Text.Types.V128, Ast.Text.Types.V128 ->
      true
  | Ref r1, Ref r2 -> eq_reftype r1 r2
  | Tuple l1, Tuple l2 ->
      List.length l1 = List.length l2 && List.for_all2 eq_valtype l1 l2
  | _ -> false

let eq_functype (f1 : Ast.Text.functype) (f2 : Ast.Text.functype) =
  let p1 = Array.map snd f1.params in
  let p2 = Array.map snd f2.params in
  Array.length p1 = Array.length p2
  && Array.for_all2 eq_valtype p1 p2
  && Array.length f1.results = Array.length f2.results
  && Array.for_all2 eq_valtype f1.results f2.results

let check_syntax diagnostics (_, lst) =
  let types = Hashtbl.create 16 in
  let functions = Hashtbl.create 16 in
  let memories = Hashtbl.create 16 in
  let tables = Hashtbl.create 16 in
  let globals = Hashtbl.create 16 in
  let tags = Hashtbl.create 16 in
  let elems = Hashtbl.create 16 in
  let datas = Hashtbl.create 16 in
  let check_unbound tbl kind id =
    let>@ id : Ast.Text.name = id in
    if Hashtbl.mem tbl id.desc then
      Error.index_already_bound diagnostics ~location:id.info kind id
    else Hashtbl.add tbl id.desc ()
  in
  let types_ctx =
    {
      types = Types.create ();
      (* unused *)
      last_index = 0;
      index_mapping = Hashtbl.create 16;
      label_mapping = Hashtbl.create 16;
    }
  in
  (* Pass 1: Build Type Mappings (Explicit) *)
  let type_defs = Hashtbl.create 16 in
  let type_defs_text = Hashtbl.create 16 in
  List.iter
    (fun field ->
      match field.Ast.desc with
      | Ast.Text.Types lst ->
          Array.iter
            (fun (id, subtype) ->
              let idx = types_ctx.last_index in
              let>@ def = comptype diagnostics types_ctx subtype.Ast.Text.typ in
              let mapping = (idx, []) in
              Hashtbl.add types_ctx.index_mapping (Uint32.of_int idx) mapping;
              Option.iter
                (fun id ->
                  check_unbound types "type" (Some id);
                  Hashtbl.replace types_ctx.label_mapping id.desc mapping)
                id;
              (Hashtbl.add type_defs idx def;
               match subtype.Ast.Text.typ with
               | Ast.Text.Types.Func f ->
                   Hashtbl.add type_defs_text idx (Ast.Text.Types.Func f)
               | _ -> ());
              types_ctx.last_index <- idx + 1)
            lst
      | _ -> ())
    lst;

  (* Helper to find reusable type *)
  let find_existing_type sign =
    (* Helper: check if comptype matches signature *)
    let sig_matches t s =
      match t with
      | Ast.Binary.Types.Func { params; results } ->
          let { Ast.Binary.Types.params = p; results = r } = s in
          p = params && r = results
      | _ -> false
    in
    let*@ target = signature diagnostics types_ctx sign in
    (*ZZZ Slow*)
    let rec loop i =
      if i >= types_ctx.last_index then None
      else
        match Hashtbl.find_opt type_defs i with
        | Some t -> if sig_matches t target then Some i else loop (i + 1)
        | None -> loop (i + 1)
    in
    loop 0
  in

  (* Helper to add implicit type *)

  (* Pass 2: Collect Implicit Types *)
  let add_implicit_type ft =
    let params = Array.map (fun (_, t) -> (None, t)) ft.Ast.Text.params in
    let results = ft.Ast.Text.results in
    let sign = { Ast.Text.params; results } in
    match find_existing_type sign with
    | Some _ -> () (* Reuse *)
    | None ->
        let idx = types_ctx.last_index in
        let>@ ty = signature diagnostics types_ctx sign in
        let def = Ast.Binary.Types.Func ty in
        Hashtbl.add type_defs idx def;
        Hashtbl.add type_defs_text idx (Ast.Text.Types.Func sign);
        Hashtbl.add types_ctx.index_mapping (Uint32.of_int idx) (idx, []);
        types_ctx.last_index <- idx + 1
  in

  let rec iter_instrs f instrs =
    List.iter
      (fun i ->
        f i.Ast.desc;
        match i.Ast.desc with
        | Ast.Text.Block { block; _ }
        | Ast.Text.Loop { block; _ }
        | Ast.Text.TryTable { block; _ } ->
            iter_instrs f block
        | Ast.Text.If { if_block; else_block; _ } ->
            iter_instrs f if_block;
            iter_instrs f else_block
        | Ast.Text.Try { block; catches; catch_all; _ } ->
            iter_instrs f block;
            List.iter (fun (_, c) -> iter_instrs f c) catches;
            Option.iter (iter_instrs f) catch_all
        | Ast.Text.Folded (instr, instrs') -> iter_instrs f (instr :: instrs')
        | _ -> ())
      instrs
  in

  List.iter
    (fun field ->
      (match field.Ast.desc with
      | Ast.Text.Import { desc; _ } -> (
          match desc with
          | Func (None, Some sign) -> add_implicit_type sign
          | Tag (None, Some sign) -> add_implicit_type sign
          | _ -> ())
      | Func { typ = None, Some sign; _ } -> add_implicit_type sign
      | Tag { typ = None, Some sign; _ } -> add_implicit_type sign
      | _ -> ());
      match field.Ast.desc with
      | Ast.Text.Func { instrs; _ } ->
          iter_instrs
            (fun desc ->
              match desc with
              | Ast.Text.Block { typ = Some (Ast.Text.Typeuse (_, Some ft)); _ }
                ->
                  add_implicit_type ft
              | Loop { typ = Some (Ast.Text.Typeuse (_, Some ft)); _ } ->
                  add_implicit_type ft
              | If { typ = Some (Ast.Text.Typeuse (_, Some ft)); _ } ->
                  add_implicit_type ft
              | Try { typ = Some (Ast.Text.Typeuse (_, Some ft)); _ } ->
                  add_implicit_type ft
              | TryTable { typ = Some (Ast.Text.Typeuse (_, Some ft)); _ } ->
                  add_implicit_type ft
              | CallIndirect (_, (_, Some ft)) -> add_implicit_type ft
              | ReturnCallIndirect (_, (_, Some ft)) -> add_implicit_type ft
              | _ -> ())
            instrs
      | _ -> ())
    lst;

  (* Pass 4: Validation *)
  let check_inline_type idx target =
    let>@ idx' = resolve_type_index diagnostics types_ctx idx in
    match Hashtbl.find_opt type_defs_text idx' with
    | Some (Ast.Text.Types.Func f) ->
        if not (eq_functype f target) then
          Error.inline_function_type_mismatch diagnostics ~location:idx.Ast.info
            f
    | _ -> failwith "indexed type is not a function type"
  in
  let check_instr_inline desc =
    let check_typeuse = function
      | Ast.Text.Typeuse (Some idx, Some ft) -> check_inline_type idx ft
      | _ -> ()
    in
    match desc with
    | Ast.Text.Block { typ = Some t; _ } -> check_typeuse t
    | Ast.Text.Loop { typ = Some t; _ }
    | Ast.Text.If { typ = Some t; _ }
    | Ast.Text.Try { typ = Some t; _ }
    | Ast.Text.TryTable { typ = Some t; _ } ->
        check_typeuse t
    | CallIndirect (_, (Some idx, Some ft)) -> check_inline_type idx ft
    | ReturnCallIndirect (_, (Some idx, Some ft)) -> check_inline_type idx ft
    | _ -> ()
  in
  let check_duplicate_locals typ locals =
    let params =
      match snd typ with
      | Some { Ast.Text.params; _ } -> Array.to_list params
      | None -> []
    in
    let all_locals = params @ locals in
    let seen = Hashtbl.create 16 in
    List.iter
      (fun (id, _) ->
        let*? id : Ast.Text.name = id in
        if Hashtbl.mem seen id.desc then failwith ("duplicate local $" ^ id.desc)
        else Hashtbl.add seen id.desc ())
      all_locals
  in
  List.iter
    (fun (field : (_ Ast.Text.modulefield, _) Ast.annotated) ->
      match field.desc with
      | Types lst ->
          Array.iter
            (fun (_, subtype) ->
              match subtype.Ast.Text.typ with
              | Ast.Text.Types.Func _ | Array _ -> ()
              | Struct lst ->
                  let fields = Hashtbl.create 16 in
                  Array.iter
                    (fun (id, _) -> check_unbound fields "field" id)
                    lst)
            lst
      | Import { id; desc; _ } -> (
          let tbl, kind =
            match desc with
            | Func _ -> (functions, "function")
            | Memory _ -> (memories, "memory")
            | Table _ -> (tables, "table")
            | Global _ -> (globals, "global")
            | Tag _ -> (tags, "tag")
          in
          check_unbound tbl kind id;
          match desc with
          | Func (Some idx, Some sign) -> check_inline_type idx sign
          | Tag (Some idx, Some sign) -> check_inline_type idx sign
          | _ -> ())
      | Func { id; typ; locals; instrs; _ } ->
          check_unbound functions "function" id;
          (match typ with
          | Some idx, Some sign -> check_inline_type idx sign
          | _ -> ());
          check_duplicate_locals typ locals;
          iter_instrs check_instr_inline instrs
      | Memory { id; _ } -> check_unbound memories "memory" id
      | Table { id; _ } -> check_unbound tables "table" id
      | Tag { id; typ = Some idx, Some sign; _ } ->
          check_unbound tags "tag" id;
          check_inline_type idx sign
      | Tag { id; _ } -> check_unbound tags "tag" id
      | Global { id; _ } -> check_unbound globals "global" id
      | Export _ | Start _ -> ()
      | Elem { id; _ } -> check_unbound elems "elem" id
      | Data { id; _ } -> check_unbound datas "data" id
      | String_global { id; _ } -> check_unbound globals "global" (Some id))
    lst;
  ignore
    (List.fold_left
       (fun can_import (field : (_ Ast.Text.modulefield, _) Ast.annotated) ->
         match (can_import, field.desc) with
         | Some previous, Import _ ->
             Error.import_after_definition diagnostics ~location:field.info
               previous;
             can_import
         | None, Func _ -> Some "function"
         | None, Memory _ -> Some "memory"
         | None, Table _ -> Some "table"
         | None, Tag _ -> Some "tag"
         | None, Global _ -> Some "global"
         | None, String_global _ -> Some "string"
         | ( Some _,
             (Func _ | Memory _ | Table _ | Tag _ | Global _ | String_global _)
           )
         | None, Import _
         | _, (Types _ | Export _ | Start _ | Elem _ | Data _) ->
             can_import)
       None lst);
  (*ZZZ*)
  assert (
    List.length
      (List.filter
         (fun field ->
           match field.Ast.desc with Ast.Text.Start _ -> true | _ -> false)
         lst)
    <= 1)
